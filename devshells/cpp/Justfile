# NOTE: Documentation: https://just.systems

set quiet
bin := "main"
test_bin := "testRunner"

default:
    just --list

format: format-cpp format-py
lint: lint-cpp lint-py

format-cpp +files="src/*.cpp include/*.h tests/*.cpp":
    echo "  FORMAT  {{ files }}"
    clang-format -i {{ files }}

# --fix
lint-cpp args="" +files="src/*.cpp include/*.h tests/*.cpp":
    echo "  LINT    {{ files }}"
    clang-tidy -p . {{ files }} {{ args }}

format-py +files=".":
    echo "  FORMAT  tests/*.py"
    ruff format {{ files }}

# --fix
lint-py args="" +files=".":
    echo "  LINT    tests/*.py"
    ruff check {{ files }} {{ args }}

# release/asan/debug
build b_type="release":
    make all BUILD_TYPE={{ b_type }}

# release/asan/debug + arguments
run b_type="release" +args="": (build b_type)
    echo "  RUN     build/{{ b_type }}/{{ bin }}"
    echo "=== RESULT ==="
    ./build/{{ b_type }}/{{ bin }} {{ args }}
    echo "=> Returned $?."

test: gtest pytest
    echo "  PASSED  tests/"

gtest:
    make BUILD_TYPE=debug build/debug/{{ test_bin }}
    echo "  GTEST   tests/"
    ./build/debug/{{ test_bin }}

pytest: (build "debug")
    echo "  PYTEST  tests/"
    pytest

entr mode="gtest":
    fd . src/ include/ tests/ | entr -c bash -c "just {{ mode }}"

valgrind +args="": (build "debug")
    echo "  PREP      valgrind"
    echo "  VALGRIND  build/debug/main"
    echo "=== RESULT ==="
    valgrind --leak-check=full        \
    --show-leak-kinds=all             \
    --track-origins=yes               \
    --verbose                         \
    --log-file=build/valgrind-out.txt \
    ./build/debug/{{ bin }} {{ args }}
    echo "  DONE      valgrind"

report: (build "debug")
    echo "  REPORT  tests/"
    pytest -vvv --md-report --md-report-verbose=2 --md-report-show-duration --md-report-output=REPORT.md

bear: clean
    echo "  BEAR    compile_commands.json"
    bear -- make BUILD_TYPE=debug all build/debug/{{ test_bin }}

clean:
    #!/usr/bin/env bash
    [[ -e build ]]                 && echo "  CLEAN   build/"                && rm -rf build                || true
    [[ -f compile_commands.json ]] && echo "  CLEAN   compile_commands.json" && rm -f compile_commands.json || true
    [[ -f REPORT.md ]]             && echo "  CLEAN   REPORT.md"             && rm -f REPORT.md             || true
    [[ -f .coverage ]]             && echo "  CLEAN   .coverage"             && rm -f .coverage             || true
