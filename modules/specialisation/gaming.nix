{
  flake.nixosModules."gaming" =
    {
      lib,
      pkgs,
      ...
    }:

    {
      specialisation = {
        "Gaming".configuration = {
          system.nixos.tags = [ "Gaming" ];

          boot.kernelPackages = lib.mkForce pkgs.cachyosKernels.linuxPackages-cachyos-latest-x86_64-v3;

          # Proton-GE performance boost
          boot.kernelModules = [ "ntsync" ];
          services.udev.extraRules = ''
            KERNEL=="ntsync", MODE="0666"
          '';

          hardware.nvidia = {
            prime = {
              sync.enable = lib.mkForce true;

              offload = {
                enable = lib.mkForce false;
                enableOffloadCmd = lib.mkForce false;
              };
            };

            powerManagement.finegrained = lib.mkForce false;
          };

          environment = {
            sessionVariables = {
              LIBVA_DRIVER_NAME = lib.mkForce "nvidia";
              GBM_BACKEND = "nvidia-drm";
              __GLX_VENDOR_LIBRARY_NAME = "nvidia";
            };

            # systemPackages = with pkgs; [ mangohud ];
          };

          nixpkgs.config.allowUnfreePredicate =
            pkg:
            builtins.elem (lib.getName pkg) [
              "steam"
              "steam-unwrapped"
            ];
          programs = {
            steam = {
              enable = true;

              gamescopeSession.enable = true;
              extraCompatPackages = with pkgs; [ proton-ge-bin ];
            };

            gamemode.enable = true;
          };
        };
      };
    };
}
